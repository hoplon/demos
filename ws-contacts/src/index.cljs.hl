(page "index.html"
      (:require [clojure.string :as string]
                [app.websocket :as ws]))

(def my-contacts
  (cell #{}))

(defn get-state []
      (ws/chsk-send! [:contacts/get-contacts {:resp-id :contacts/initialize}]))

(defn delete-contact [contact]
      (ws/chsk-send! [:contacts/delete {:contact contact}]))

(defn add-contact [contact]
      (ws/chsk-send! [:contacts/add {:contact contact}]))

(defn middle-name [{:keys [middle middle-initial]}]
      (cond
        middle (str " " middle)
        middle-initial (str " " middle-initial ".")))

(defn display-name [{:keys [first last] :as contact}]
      (str last ", " first (middle-name contact)))

(defelem contact-list [{:keys [from sorted-by] :or {sorted-by identity}}]
         (loop-tpl :bindings [contact (cell= (sort-by sorted-by from))]
                   (li (span (cell= (display-name contact)))
                       (button :click #(delete-contact @contact) "Delete"))))

(defn parse-contact [contact-str]
      (let [[first middle last :as parts] (string/split contact-str #"\s+")
            [first last middle] (if (nil? last) [first middle] [first last middle])
            middle (when middle (string/replace middle "." ""))
            c (if middle (count middle) 0)]
           (when (>= (count parts) 2)
                 (cond-> {:first first :last last}
                         (== c 1) (assoc :middle-initial middle)
                         (>= c 2) (assoc :middle middle)))))

(defelem contact-input [{:keys [to]} [label]]
         (let [new-contact (cell "")
               parsed      (cell= (parse-contact new-contact))]
              (div
                (input
                  :value new-contact
                  :input #(reset! new-contact @%))
                (button
                  :click #(when-let [c @parsed]
                                    (dosync (to c)
                                            (reset! new-contact "")))
                  :disabled (cell= (not parsed))
                  label)
                (pre (cell= (pr-str parsed))))))

(defmethod ws/chsk-recv :contacts/initialize
           [id contacts]
           (reset! my-contacts contacts))

(defmethod ws/chsk-recv :contacts/deleted
           [id contact]
           (swap! my-contacts disj contact))

(defmethod ws/chsk-recv :contacts/added
           [id contact]
           (swap! my-contacts conj contact))

(ws/on-open get-state)

(ws/start!)

(html
  (head
    (link :rel "stylesheet" :type "text/css" :href "css/main.css"))
  (body
    (h2 "Contact list")
    (ul (contact-list :from my-contacts :sorted-by :last))
    (contact-input :to add-contact "Add contact")
    (hr)
    (p (em "Note: The Add contact button is disabled until you enter a valid contact.  A valid contact consists of two or three whitespace-delimited names."))
    (a :href "https://github.com/tailrecursion/hoplon-demos/tree/master/contacts" "Source code on Github")))
